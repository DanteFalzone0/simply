/*
	Commodore64 random maze example
	Reimplements the following code:
	10 PRINT CHR$(205.5+RND(1)); : GOTO 10
	
	However, only draws a single screen.
	Click/tap on the canvas to regenerate the maze.
	Please check other examples to understand how this works.
*/

unless !CANVAS then {
	echo "❌ The canvas module is required for this example";
	return;
}

$url = &join([
	"data:image/png;base64,iVBORw0KGgoAAAANSUhEUgAAACgA",
	"AAAIAQMAAACxnC8YAAAABlBMVEVgSe2ij/+XgPdPAAAACXBIWX",
	"MAAA7DAAAOwwHHb6hkAAAAMUlEQVQI1w3KMREAMAjAwL9jYEMB",
	"QiqrEpFSKWXJkERMDPnyUbcuffqwaFYUG5MdwwfzqAnxR+ME1w",
	"AAAABJRU5ErkJggg=="
], '');

%fg = "rgb(162, 143, 255)";
%bg = "rgb(96, 73, 237)";

%c = call !CANVAS->init(384, 272, %fg);

call %c->setCoordMode("grid", 8); //8x8
call %c->fillRect(%x = 4, %y = 4.5, %width = 40, %height = 25, %bg);

def &fn($sheet) {
	if $sheet->error {
		echo "❌ Failed to load the sprite sheet";
		return;
	}
	
	%sheet = $sheet;
	%drawing = false;
	
	def &draw {
		if %drawing return;
		
		%drawing = true;
		call %c->suspendUpdates();
		
		$array = [];
		$count = &sub(%sheet->length, 1);

		for $i in 1..%height {
			for $j in 1..%width {
				$array[$j] = &rand(0, $count);
			}
			
			call %sheet->drawSpriteLine($array, %x, &add(%y, $i, -1));
		}
		
		call %c->resumeUpdates();
		%drawing = false;
	}
	
	call %c->onup(&draw);
	&draw();
}

// $url, $width and $height of the sprites and a function
call %c->createSpritesheet($url, 1, 1, &fn);
